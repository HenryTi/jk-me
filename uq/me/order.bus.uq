BUS JkOrderBus ver 0.5 from 百灵威系统工程部/[jk-order]
ACCEPT order {
	VAR mainId ID;
	SET mainId = id;
	WITH OrderMain as a ID=mainId SET a.no=no, a.customer=customer;
	FOR detail {
		WITH OrderDetail as a 
			ID = id
			SET a.main=mainId
				, a.item=item, a.product=product
				, a.quantity=quantity, a.price=price
				, a.amount=quantity * price;
	}
    SCHEDULE ActOrder;
}
ACCEPT [deliver-done] {
	FOR detail {
		WITH DxOrderDetail ID=id SET deliverDone=value;
        VAR itemHistoryId ID = ID(ItemHistory new, id, Item.orderDeliver);
        WITH ItemHistory as a ID = itemHistoryId
			SET a.value=value;
        WITH IxPendingOrderItemAction IX=id XI=EnumOrderAction.[deliverDone] set value = value;
	}
    SCHEDULE ActOrder;
}
ACCEPT [receive-done] {
	FOR detail {
		WITH DxOrderDetail ID=id SET receiveDone+=value;
        VAR itemHistoryId ID = ID(ItemHistory new, id, Item.orderReceive);
        WITH ItemHistory as a ID = itemHistoryId
			SET a.value=value;
        WITH IxPendingOrderItemAction IX=id XI=EnumOrderAction.[receiveDone] set value = value;
	}
    SCHEDULE ActOrder;
}
ACCEPT [return] {
	FOR detail {
		WITH DxOrderDetail ID=id SET return+=quantity;
        WITH IxPendingOrderItemAction IX=id XI=EnumOrderAction.[return] set value = quantity;
	}
    SCHEDULE ActOrder;
}
ACCEPT [order-cost-price] {
    FOR detail {
        WITH DxOrderDetail ID=[order-detail] SET costPrice=[cost-price];
    }
    WITH DxOrderMain ID=order SET flagCostPrice=1;
    SCHEDULE ActOrder;
}
ACCEPT [order-bottom-price] {
    FOR detail {
        WITH DxOrderDetail ID = [order-detail] SET bottomPrice = [bottom-price];
        WITH DxOrderDetailFlag ID = [order-detail] SET flagBottomPrice = 1;
    }
    SCHEDULE ActOrder;
}
-- 订单提成对象绑定
ACCEPT [order-bound] {
    FOR to {
		VAR toId ID;
		IF [id-type] = 'staff' {
			SET toId=ID(ObjectStaff, id);
			IF toId IS NULL {
				SET toId=ID(Object new);
				WITH ObjectStaff ID=toId SET staff=id;
			}
		}
		ELSEIF [id-type] = 'customer' {
			SET toId=ID(ObjectCustomer, id);
			IF toId IS NULL {
				SET toId=ID(Object new);
				WITH ObjectCustomer ID=toId SET customer=id;
			}
		}
		ELSE {
			SET toId = id;
		}
        WITH IxOrderBoundTo IXX=order IX=textid(type) XI=toId;
    }
    -- 这个地方是不是有漏洞，如果是对同一个order发送了两个order-bound，一个绑staff，一个绑customer
    -- 那就可能会漏掉一个吧(要么就是对同一个order的绑定必须一次发过来，这个要求好像不合理)
    WITH DxOrderMain ID=order SET flagBoundTo=1;    
    SCHEDULE ActOrder;
}
;
