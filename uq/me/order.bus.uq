BUS JkOrderBus ver 0.7 from 百灵威系统工程部/[jk-order]
ACCEPT order {
	VAR mainId ID;
	SET mainId = id;
    LOG CONCAT_WS(' ', mainId) SUBJECT 'JkOrderBus ACCEPT order';
	WITH OrderMain as a ID=mainId SET a.no=no, a.customerAccount=customerAccount;
	FOR detail {
        LOG CONCAT_WS(' ', id, quantity, item) SUBJECT 'JkOrderBus ACCEPT orderDetail';
		WITH OrderDetail as a 
			ID = id
			SET a.main=mainId
				, a.item=item, a.product=product
				, a.quantity=quantity
                , a.price=price
				, a.amount=amount;
	}

    -- 所有的订单绑定到ManagerIT
    VAR objManagerId ID;
    SET objManagerId=ID(ObjectPost, Post.managerIT);
    IF objManagerId IS NULL {
        SET objManagerId=ID(Object new);
        WITH ObjectPost as a ID=objManagerId SET a.post=Post.managerIT;
    }
    WITH IxBizMainBoundTo IXX=mainId IX=Post.managerIT XI=objManagerId;

    PROC ProcSetOrderReady(mainId, OrderReady.sheet);
    PROC ProcQueueBizMain(mainId);
}
ACCEPT [deliver-done] {
	FOR detail {
        LOG CONCAT_WS(' ', orderDetail, value) SUBJECT 'ACCEPT JkOrderBus.deliver-done';
		WITH DxOrderDetail ID=orderDetail SET deliverDone += value;
        WITH DxBizOp as a ID=id 
            SET a.action=EnumBizAction.orderDeliverDone, a.biz=orderDetail, a.value=value;
        VAR queueId ID = ID(QueueBizOp new, id);
	}
    SCHEDULE ExecQueueBizOp DELAY 1;
}
ACCEPT [receive-done] {
	FOR detail {
        LOG CONCAT_WS(' ', orderDetail, value) SUBJECT 'ACCEPT JkOrderBus.receive-done';
		WITH DxOrderDetail ID=orderDetail SET receiveDone+=value;
        WITH DxBizOp as a ID=id 
            SET a.action=EnumBizAction.orderReceiveDone, a.biz=orderDetail, a.value=value;
        VAR queueId ID = ID(QueueBizOp new, id);
	}
    SCHEDULE ExecQueueBizOp DELAY 1;
}
ACCEPT [return] {
	FOR detail {
		WITH DxOrderDetail ID=orderDetail SET return+=quantity;
        WITH DxBizOp as a ID=id 
            SET a.action=EnumBizAction.orderReturn, a.biz=orderDetail, a.value=quantity;
        VAR queueId ID = ID(QueueBizOp new, id);
	}
    SCHEDULE ExecQueueBizOp DELAY 1;
}
ACCEPT [order-sale-cost] {
    LOG CONCAT_WS(' ', order) SUBJECT 'JkOrderBus ACCEPT order-sale-cost';
    FOR detail {
        LOG CONCAT_WS(' ', [order-detail], [cost]) SUBJECT 'ACCEPT JkOrderBus.order-sale-cost';
        WITH DxOrderDetail ID=[order-detail] SET costPrice=[cost];
    }
    PROC ProcSetOrderReady(order, OrderReady.cost);
    PROC ProcQueueBizMain(order);
}
ACCEPT [order-bound-staff-sales] {
    LOG CONCAT_WS(' ', order) SUBJECT 'JkOrderBus ACCEPT order-bound-staff-sales';
    FOR to {
		VAR toId ID;
        SET toId=ID(ObjectStaff, id);
        IF toId IS NULL {
            SET toId=ID(Object new);
            WITH ObjectStaff ID=toId SET staff=id;
        }
        WITH IxBizMainBoundTo IXX=order IX=Post.staffSales XI=toId;
    }
    PROC ProcQueueBizMain(order);
}
ACCEPT [order-bound-agent] { -- 轻代理
    FOR to {
		VAR toId ID;
		SET toId=ID(ObjectAgent, id);
        IF toId IS NULL {
            SET toId=ID(Object new);
            WITH ObjectAgent ID=toId SET agent=id;
        }
        WITH IxBizMainBoundTo IXX=order IX=Post.agent XI=toId;
    }
    PROC ProcQueueBizMain(order);
}
ACCEPT [order-bound-distributor] { -- 分销商
    FOR to {
		VAR toId ID;
        SET toId=ID(ObjectDistributor, id);
        IF toId IS NULL {
            SET toId=ID(Object new);
            WITH ObjectDistributor ID=toId SET distributor=id;
        }
        WITH IxBizMainBoundTo IXX=order IX=Post.distributor XI=toId;
    }
    PROC ProcQueueBizMain(order);
}
ACCEPT [order-bound-customer] {
    FOR to {
		VAR toId ID;
        SET toId=ID(ObjectCustomer, id);
        IF toId IS NULL {
            SET toId=ID(Object new);
            WITH ObjectCustomer ID=toId SET customer=id;
        }
        WITH IxBizMainBoundTo IXX=order IX=Post.customer XI=toId;
    }
    PROC ProcQueueBizMain(order);
}
;

PROC ProcQueueBizMain(
    bizMain ID,
) {
    IF EXISTS(SELECT id FROM DxBizMain WHERE ready=1 AND id=bizMain)
        AND NOT EXISTS(SELECT a.id FROM QueueBizMain as a WHERE a.bizMain=bizMain)
    {
        VAR queueId ID = ID(QueueBizMain new, bizMain);
        SCHEDULE ExecQueueBizMain DELAY 1;
    }
};

PROC ProcSetOrderReady(
    order ID,
    orderReady ENUM OrderReady,
) {
    VAR ready ENUM OrderReady;
    SET ready=a.readyStates FROM DxOrderMain as a WHERE a.id=order;
    IF ready IS NULL {
        SET ready = 0;
    }
    SET ready = ready | orderReady;
    WITH DxOrderMain ID=order SET readyStates=ready;
    IF ready = (OrderReady.sheet | OrderReady.cost) {
        WITH DxBizMain ID=order SET ready=1;
    }
};
