-- 每次处理最多maxActionRows行，然后退出，进入下一个循环。
-- 这样可以避免数据库被占用太多算力，以至于无法响应访问操作
CONST maxActionRows = 10;

ACT ActOrderAction() {
    VAR orderActionId ID, rowCount INT = 0;
    SETTING 'orderActionId' BIGINT TO orderActionId;
    IF orderActionId IS NULL {
        SET orderActionId = 0;
    }
    FOR (VAR id ID, actionId ID, action ENUM EnumOrderAction
        , value DEC(18,4), item ENUM Item
        OF SELECT a.id, a.actionId, a.action, a.value, a.item
            FROM OrderAction as a
            WHERE a.id>orderActionId
            ORDER BY a.id ASC
            LIMIT maxActionRows)
    {
        IF action>=EnumOrderAction.orderMain {
            FOR (VAR orderActionId ID
                OF SELECT a.id as orderActionId
                    FROM DxOrderAction as a
                    WHERE a.orderMain=actionId)
            {
                -- 处理跟这个订单相关的所有action
                PROC ProcOrderAction(orderActionId);
            }
        }
        ELSE {
            VAR itemHistoryId ID = ID(ItemHistory new, actionId, item);
            WITH ItemHistory as a ID = itemHistoryId SET a.value=value;
            PROC ProcOrderAction(id);
        }
        SET rowCount = rowCount+1;
        SETTING 'orderActionId' BIGINT = id;
    }

    IF rowCount>=maxActionRows {
        SCHEDULE ActOrderAction;
    }
};

PROC ProcOrderAction(
    orderActionId ID,
) {
    VAR orderMain ID OrderMain,
        orderDetail ID OrderDetail,             -- 如果0，则是针对Order主表
        actionId ID,                            -- deliver, receive, return detail id
        action ENUM EnumOrderAction,
        value DEC(18,4),
        done ENUM EnumDone;

    SET orderMain=a.orderMain
        , orderDetail=a.orderDetail
        , done=a.done
        FROM DxOrderAction as a WHERE a.id=orderActionId;    

    -- 表示没有DxOrderAction对应的内容。
    IF orderDetail IS NULL {
        set done=0;
        SET orderDetail=a.orderDetail FROM OrderAction as a WHERE a.id=orderActionId;
        SET orderMain=a.main FROM OrderDetail as a WHERE a.id=orderDetail;
        IF orderMain IS NULL {
            SET orderMain = 0;
        }
        WITH DxOrderAction as a ID=orderActionId
            SET a.orderMain=orderMain, a.orderDetail=orderDetail;
    }
    

    IF orderMain=0 {
        SET orderMain=a.main FROM OrderDetail as a WHERE a.id=orderDetail;
        IF orderMain IS NULL { RETURN; }
        WITH DxOrderAction as a ID=orderActionId SET a.orderMain=orderMain;
    }

    SET actionId=a.actionId, action=a.action, value=a.value 
        FROM OrderAction as a WHERE a.id=orderActionId;

    VAR readyStates ENUM ReadyStates;
    SET readyStates=a.readyStates FROM DxOrderMain as a WHERE a.id=orderMain;

    VAR nDone ENUM EnumDone = done;
    IF (readyStates&ReadyStates.cost)=ReadyStates.cost AND (done & EnumDone.withCost)=0 {
        PROC ProcBoundPost(action, Post.none, 0, actionId, readyStates, value);
        SET nDone = done & EnumDone.withCost;
    }
    IF (done & EnumDone.withoutCost)=0 {
        PROC ProcBoundPost(action, Post.none, 0, actionId, readyStates, value);
        SET nDone = done & EnumDone.withoutCost;
    }
    IF done<>nDone {
        WITH DxOrderAction as a ID=orderActionId SET a.done=nDone;
    }

    FOR (VAR post ENUM Post, to ID
        OF SELECT a.ix as post, a.xi as to
            FROM IxOrderBoundTo as a 
            WHERE a.ixx=orderMain
        )
    {
        VAR doneBound TINYINT;
        SET doneBound=a.done FROM IxOrderBoundPost as a WHERE a.ix=orderMain AND a.xi=post;
        IF doneBound IS NULL { 
            SET doneBound=0; 
        }
        ELSEIF doneBound=(EnumDone.withCost|EnumDone.withoutCost) {
            CONTINUE;
        }
        IF (readyStates&ReadyStates.cost)=ReadyStates.cost AND (doneBound & EnumDone.withCost)=0 {
            PROC ProcBoundPost(action, post, to, actionId, readyStates&ReadyStates.cost, value);
            SET doneBound = doneBound | EnumDone.withCost;
        }
        IF (done & EnumDone.withoutCost)=0 {
            PROC ProcBoundPost(action, post, to, actionId, readyStates&ReadyStates.costNone, value);
            SET doneBound = doneBound | EnumDone.withoutCost;
        }
        WITH IxOrderBoundPost as a IX=orderMain XI=post SET a.done=doneBound;
    }
};

PROC SaveObjHistory(
    action ENUM EnumOrderAction, 
    actionId ID,
    post ENUM Post, 
    to ID, 
    objectHistoryItem ENUM Item, 
    itemHistoryItem ENUM Item, 
    value DEC(18,4),
) {
    VAR itemHistoryId ID = ID(ItemHistory new, actionId, itemHistoryItem);
    WITH ItemHistory as a ID = itemHistoryId SET a.value=value;

    VAR objectPostItemId ID = ID(ObjectPostItem new, to, post, objectHistoryItem);
    VAR ratio DEC(6,2) = NULL;
    SET ratio = a.ratio FROM PostItem as a 
        WHERE a.IXX=post AND a.IX=itemHistoryItem AND a.XI=objectHistoryItem;
    WITH PostItemHistory as a IX=objectPostItemId XI=itemHistoryId 
        SET a.value=value * IFNULL(ratio, 100) / 100, a.action=action;
};

PROC ProcBoundPost (
    action ENUM EnumOrderAction, 
    post ENUM Post, 
    to ID, 
    actionId ID, 
    readyStates ENUM ReadyStates,        -- 15种ReadyStates，按位记
    value DEC(18,4),
) {
    FOR (VAR item ENUM Item, itemToObj ENUM Item
        OF SELECT a.item, a.itemToObj FROM PostProc as a 
            WHERE a.post=post AND a.action=action AND a.readyStates=readyStates)
    {
        VAR itemHistoryId ID = ID(ItemHistory new, actionId, item);
        WITH ItemHistory as a ID = itemHistoryId SET a.value=value;

        IF NOT itemToObj IS NULL {
            VAR objectPostItemId ID = ID(ObjectPostItem new, to, post, itemToObj);
            VAR ratio DEC(6,2) = NULL;
            SET ratio = a.ratio FROM PostItem as a 
                WHERE a.IXX=post AND a.IX=itemToObj AND a.XI=item;
            WITH PostItemHistory as a IX=objectPostItemId XI=itemHistoryId 
                SET a.value=value * IFNULL(ratio, 100) / 100, a.action=action;
        }
    }
};
